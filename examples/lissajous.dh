include "std/gui"
include "std/drw"
include "std/math"
include "std/str"

@param                  title := "Lissajous"
@param[1,10]            frq_a := 3;
@param[1,10]            frq_b := 2;
@param[{0.,0.},{1.,1.}] scale := {1.0,1.0};
@param[1.0,5.0]         thick := 2.0;
@param[0,1]             pause := 0;
@param[0.001,0.01]      speed := 0.005;

embed global as "gui/layout"

phase := 0.0

drw.size(256,256);
while (1){
  embed global as "gui/sync"
  drw.background(0);
  drw.no_fill();
  drw.stroke(1.0);
  drw.stroke_weight(thick);
  n := 500;
  drw.begin_shape();
  for (i:=0; i<=n; i++){
    ang := (i/(n as f32))*math.PI*2;
    x := 100 * scale.x * math.sin(frq_a * ang + phase);
    y := 100 * scale.y * math.sin(frq_b * ang);
    drw.vertex(128+x,128+y);
  }
  drw.end_shape();
  if (!pause){
    phase += speed;
  }
  drw.fill(1.0);
  drw.text(title,128-title.length()*4,250);

  drw.poll();
}