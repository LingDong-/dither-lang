include "std/gui"
include "std/gx"
include "std/math"
include "std/str"

@param                  title := "Lissajous"
@param[1,10]            frq_a := 3;
@param[1,10]            frq_b := 2;
@param[{0.,0.},{1.,1.}] scale := {1.0,1.0};
@param[1.0,5.0]         thick := 2.0;
@param[0,1]             pause := 0;
@param[0.001,0.01]      speed := 0.005;

embed global as "gui/layout"

phase := 0.0

gx.size(256,256);
while (1){
  embed global as "gui/sync"
  gx.background(0);
  gx.no_fill();
  gx.stroke(1.0);
  gx.stroke_weight(thick);
  n := 500;
  gx.begin_shape();
  for (i:=0; i<=n; i++){
    ang := (i/(n as f32))*math.PI*2;
    x := 100 * scale.x * math.sin(frq_a * ang + phase);
    y := 100 * scale.y * math.sin(frq_b * ang);
    gx.vertex(128+x,128+y);
  }
  gx.end_shape();
  if (!pause){
    phase += speed;
  }
  gx.fill(1.0);
  gx.text(title,128-title.length()*4,250);

  gx.poll();
}