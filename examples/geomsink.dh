
include "std/geom"
include "std/io"
include "std/drw"
include "std/list"
include "std/win"
include "std/time"

polygon := list[vec[f32,2]]{
  { 89.5, 42.5 },   { 137.5, 45.5 },   { 183, 34 },
  { 220.5, 16 },    { 267.5, 55.5 },   { 202.5, 104.5 },
  { 151.5, 156 },   { 223.5, 139.5 },  { 223.5, 123.5 },
  { 269.5, 139 },   { 293, 182 },      { 249.5, 178 },
  { 180, 185.5 },   { 194.5, 225.5 },  { 233.5, 227 },
  { 254, 258.5 },   { 190.5, 277.5 },  { 189, 315 },
  { 250, 310.5 },   { 320, 318.5 },    { 333, 356 },
  { 273.5, 349.5 }, { 208, 346.5 },    { 149.5, 355.5 },
  { 92.5, 371 },    { 41.5, 385 },     { 11, 346 },
  { 52, 345 },      { 138.5, 322 },    { 143.5, 300 },
  { 121.5, 308 },   { 82, 294.5 },     { 80.5, 258.5 },
  { 114.5, 256.5 }, { 145, 240 },      { 134.5, 200.5 },
  { 92, 220 },      { 48.5, 199.5 },   { 55, 163.5 },
  { 77.5, 164.5 },  { 144.5, 102 },    { 124, 93.5 }
}

tris := geom.triangulate(polygon);
hull := geom.convex_hull(polygon);
samp := geom.poly_resample(polygon,20.0,geom.BY_SPACING|geom.MODE_POLYGON)
simp := geom.poly_simplify(samp,50.0);
dlny := geom.delaunay(polygon);
voro := geom.voronoi(polygon);
[cen,ext,axs] := geom.bbox(polygon, geom.MODE_ORIENTED|1);

drw.size(500,400);

show := list[i32]{0,0,0,0,0,0,0,0,0,0};
desc := list[str]{
  "input shape",
  "triangulation",
  "convex hull",
  "point-in-polygon test",
  "resample",
  "polygon simplification",
  "delaunay triangulation",
  "voronoi diagram",
  "catmull-rom spline",
  "oriented bounding box",
}

frame := 0;

while (1){
  time.fps(120);
  drw.background(0.95);

  if (show[0]){
    drw.no_stroke();
    drw.fill(1.0,0.75,0.75);
    drw.begin_shape();
    for (i := 0; i < polygon.length(); i++){
      drw.vertex(...polygon[i]);
    }
    drw.end_shape(1);
  }

  if (show[1]){
    drw.stroke(0);
    drw.stroke_weight(1);
    drw.no_fill();
    for (i := 0; i < tris.length(); i+=3){
      drw.begin_shape();
      drw.vertex(...polygon[tris[i  ]]);
      drw.vertex(...polygon[tris[i+1]]);
      drw.vertex(...polygon[tris[i+2]]);
      drw.end_shape(1);
    }
  }

  if (show[2]){
    drw.stroke(0);
    drw.stroke_weight(3);
    drw.begin_shape();
    drw.no_fill();
    for (i := 0; i < hull.length(); i++){
      drw.vertex(...hull[i]);
    }
    drw.end_shape(1);
    for (i := 0; i < hull.length(); i++){
      drw.ellipse(...hull[i],4,4);
    }
  }

  if (show[3]){
    drw.no_fill();
    drw.stroke_weight(1);
    drw.stroke(0,0.5,0);
    for (i := 0; i < 400; i+=10){
      for (j := 0; j < 400; j+=10){
        if (geom.pt_in_poly({i,j},polygon)){
          drw.ellipse(i,j,4,4);
        }
      }
    }
  }

  if (show[4]){
    drw.stroke(1,0,0);
    drw.stroke_weight(1);
    drw.no_fill();
    for (i := 0; i < samp.length(); i++){
      drw.ellipse(...samp[i],20,20);
    }
  }

  if (show[5]){
    drw.stroke(0,0.5,0.5);
    drw.stroke_weight(2);
    drw.no_fill();
    drw.begin_shape();
    for (i := 0; i < simp.length(); i++){
      drw.vertex(...simp[i]);
    }
    drw.end_shape(1);
  }

  if (show[6]){
    drw.stroke_weight(1);
    drw.stroke(1,0,1);
    drw.no_fill();
    for (i := 0; i < dlny.length(); i+=3){
      drw.begin_shape();
      drw.vertex(...polygon[dlny[i  ]]);
      drw.vertex(...polygon[dlny[i+1]]);
      drw.vertex(...polygon[dlny[i+2]]);
      drw.end_shape(1);
    }
  }

  if (show[7]){
    drw.stroke_weight(1);
    drw.stroke(0,0,1);
    drw.no_fill();
    for (i := 0; i < voro.length(); i++){
      drw.begin_shape();
      for (j := 0; j < voro[i].length(); j++){
        drw.vertex(...(voro[i][j]));
      }
      drw.end_shape(1);
    }

    drw.stroke(0,0,1);
    for (i := 0; i < polygon.length(); i++){
      drw.ellipse(...polygon[i],4,4);
    }
  }

  if (show[8]){
    drw.stroke_weight(2);
    drw.no_fill();
    drw.stroke(1.0,0.33,0);
    drw.begin_shape();
    pms := list[f32]{0.75};
    for (i:=0;i<polygon.length();i++){
      pts := list[vec[f32,2]]{
        polygon[i], 
        polygon[(i+1)%polygon.length()], 
        polygon[(i+2)%polygon.length()], 
        polygon[(i+3)%polygon.length()]
      };
      for (t:=0.0; t<1.0; t+=0.05){
        v := geom.curve(pts, pms, t, geom.TYPE_CATROM);
        drw.vertex(...v);
      }
    }
    drw.end_shape(1);
  }

  if (show[9]){
    c0 := cen+axs@*{-ext.x;-ext.y};
    c1 := cen+axs@*{ext.x;-ext.y};
    c3 := cen+axs@*{-ext.x;ext.y};
    c2 := cen+axs@*{ext.x;ext.y};
    drw.no_fill();
    drw.stroke_weight(2);
    drw.stroke(0,0.5,0);
    drw.begin_shape();
    drw.vertex(...c0);
    drw.vertex(...c1);
    drw.vertex(...c2);
    drw.vertex(...c3);
    drw.end_shape(1);
  }

  drw.fill(0);
  drw.text("Press keys to toggle...",300,16);
  for (i := 0; i < desc.length(); i++){
    drw.text("[%{i}]%{desc[i]}",300,i*16+32);
  }
  drw.text("[ESC]hide all",300,192);

  e := drw.poll();

  if (frame < 10){
    show[frame] = 1;
  }else{
    if (e.type == win.KEY_PRESSED){
      if ('0' <= e.key && e.key <= '9'){
        show[e.key - '0'] ^= 1;
      }else if (e.key == 27){
        for (i:=0;i<show.length();i++)show[i]=0;
      }
    }
  }
  frame++;
}


