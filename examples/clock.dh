include "std/time"
include "std/math"
include "std/drw"
include "std/str"

W := 256

r_face:= W*0.48
r_tick:= W*0.45
r_hour:= W*0.25
r_min := W*0.35
r_sec := W*0.42

month_names := list[str]{
  "JAN","FEB","MAR","APR","MAY","JUN","JUL","AUG","SEP","OCT","NOV","DEC"
};

drw.size(W,W);

while (1){
  drw.background(0.5);
  drw.fill(1.0);

  [year,month,day,hour,min,sec] := time.local();

  drw.push_matrix();
  drw.translate(W*0.5,W*0.5);

  drw.stroke_weight(2);
  drw.ellipse(0,0,r_face*2,r_face*2);

  s := (sec          )/60.0*math.PI*2-math.PI/2;
  m := (min +sec/60.0)/60.0*math.PI*2-math.PI/2;
  h := (hour+min/60.0)/12.0*math.PI*2-math.PI/2;

  for (i := 0; i < 60; i++){
    
    ang := i/60.0 * math.PI*2;
    if (!(i%5)){
      drw.stroke_weight(1);
      drw.line(
        r_tick*math.cos(ang),r_tick*math.sin(ang),
        r_sec *math.cos(ang),r_sec *math.sin(ang)
      );
    }else{
      drw.stroke_weight(2);
      drw.point(r_tick*math.cos(ang),r_tick*math.sin(ang));
    }
  }

  drw.stroke_weight(1);
  drw.line(0,0,r_sec *math.cos(s),r_sec *math.sin(s));

  drw.stroke_weight(2);
  drw.line(0,0,r_min *math.cos(m),r_min *math.sin(m));

  drw.stroke_weight(4);
  drw.line(0,0,r_hour*math.cos(h),r_hour*math.sin(h));

  drw.fill(0.0);
  calen := "%{month_names[month-1]} %{day}, %{year}";

  drw.text(calen,-calen.length()*4.0,W*0.25);

  drw.pop_matrix();
  drw.poll();
}