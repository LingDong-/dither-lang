include "std/font"
include "std/io"
include "std/drw"
include "std/list"

bytes := io.read_file("examples/assets/caslon.ttf");
tf := font.decode(bytes);

fsz := 150

func draw_polys(polys:list[list[vec[f32,2]]]){
  drw.begin_shape();
  for (i:=0; i < polys.length(); i++){
    for (j:=0; j < polys[i].length(); j++){
      drw.vertex(polys[i][j].x*fsz, polys[i][j].y*fsz+fsz);
    }
    drw.next_contour(1);
  }
  drw.end_shape(1);
}

drw.size(600,600);
drw.background(1.0,0.95,0.9);
drw.stroke(0.0);
{
  drw.push_matrix();
  drw.fill(0);
  drw.text("Minimal Example",0,16);
  drw.fill(0,0.5);
  draw_polys(tf.glyph(tf.lookup('a',font.FEAT_NONE)));
  drw.pop_matrix();
}

drw.translate(200,0);

{
  drw.push_matrix();
  drw.fill(0);
  drw.text("Low-Res Example",0,16);
  drw.fill(0,0.5);
  draw_polys(tf.glyph(tf.lookup('a',font.FEAT_NONE),1));
  drw.pop_matrix();
}

drw.translate(200,0);

{
  drw.push_matrix();
  drw.fill(0);
  drw.text("Kerning Example",0,16);
  drw.fill(0,0.5);
  a := tf.lookup('T',font.FEAT_NONE);
  b := tf.lookup('o',font.FEAT_NONE);
  draw_polys(tf.glyph(a));
  drw.translate(tf.advance(a,b,font.FEAT_KERN)*fsz,0);
  draw_polys(tf.glyph(b));
  drw.pop_matrix();
}

drw.translate(-400,200);

{
  drw.push_matrix();
  drw.fill(0);
  drw.text("Ligature Example",0,16);
  drw.fill(0,0.5);
  lig := list[i32]{'f','f','i'};
  draw_polys(tf.glyph(tf.lookup(lig,font.FEAT_LIGA)));
  drw.pop_matrix();
}

drw.translate(200,0);

{
  drw.push_matrix();
  drw.fill(0);
  drw.text("Old Figures Example",0,16);
  drw.fill(0,0.5);
  a := tf.lookup('3',font.FEAT_NONE);
  b := tf.lookup('3',font.FEAT_ONUM);
  draw_polys(tf.glyph(a));
  drw.translate(tf.advance(a,b,font.FEAT_KERN)*fsz,0);
  draw_polys(tf.glyph(b));
  drw.pop_matrix();
}


drw.translate(200,0);

{
  drw.push_matrix();
  drw.fill(0);
  drw.text("Non-ASCII Example",0,16);
  drw.fill(0,0.5);
  a := tf.lookup(0xdf,font.FEAT_NONE);
  b := tf.lookup(0xf1,font.FEAT_NONE);
  draw_polys(tf.glyph(a));
  drw.translate(tf.advance(a,b,font.FEAT_KERN)*fsz,0);
  draw_polys(tf.glyph(b));
  drw.pop_matrix();
}

drw.translate(-400,200);

{
  drw.push_matrix();
  drw.fill(0);
  drw.text("Multiple Ligatures Example",0,16);
  drw.fill(0,0.5);
  codes := list[i32]{'f','i','r','s','t'};
  a := -1;
  while (codes.length()){
    b := tf.lookup(codes, font.FEAT_LIGA);
    drw.translate(tf.advance(a,b,font.FEAT_KERN)*fsz,0);
    draw_polys(tf.glyph(b));
    a = b;
  }
  drw.pop_matrix();
}


drw.translate(400,0);

{
  drw.push_matrix();
  drw.fill(0);
  drw.text("Distorted Example",0,16);
  drw.fill(0,0.5);
  
  a := tf.lookup('a',font.FEAT_NONE);
  polys := tf.glyph(a);
  for (i := 0; i < polys.length(); i++){
    for (j := 0; j < polys[i].length(); j++){
      ({x,y}) := polys[i][j];
      v := (x*x+y*y)**0.5*2;
      polys[i][j] = {x,y}*v;
    }
  }
  draw_polys(polys);
  drw.pop_matrix();
}

while (1) drw.poll();
