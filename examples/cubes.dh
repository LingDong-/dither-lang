include "std/rdr"
include "std/frag"
include "std/win"
include "std/list"
include "std/arr"
include "std/time"
include "std/vec"
include "std/math"
include "std/img"
include "std/io"

W := 512;
H := 512;

context := win.init(W,H,win.CONTEXT_3D);
rdr.init(context);
frag.init(context);

func normal_material(@varying normal:vec[f32,3]):vec[f32,4]{
  c := normal*0.5+0.5;
  return {c.x,c.y,c.z,1.0};
}
func lambert_material(
  @varying normal:vec[f32,3],
  @uniform light:vec[f32,3]
):vec[f32,4]{

  l := light.dir();
  c := normal.dot(l);
  c = math.max(c,0.0)*0.75 + 0.25;
  return {c,c,c,1.0};
}
func textured_material(
  @varying uv:vec[f32,2],
  @uniform image:frag.Texture
):vec[f32,4]{

  c := image.sample(uv);
  return {c.x,c.y,c.z,1.0};
}

shader0 := frag.program(embed normal_material as "fragment");
shader1 := frag.program(embed lambert_material as "fragment");
shader2 := frag.program(embed textured_material as "fragment");

cube_vertices:=list[vec[f32,3]]{
  { 1.,-1.,-1.}, { 1., 1.,-1.}, { 1., 1., 1.}, { 1.,-1., 1.}, 
  {-1.,-1., 1.}, {-1., 1., 1.}, {-1., 1.,-1.}, {-1.,-1.,-1.}, 
  {-1., 1.,-1.}, {-1., 1., 1.}, { 1., 1., 1.}, { 1., 1.,-1.}, 
  {-1.,-1., 1.}, {-1.,-1.,-1.}, { 1.,-1.,-1.}, { 1.,-1., 1.}, 
  { 1.,-1., 1.}, { 1., 1., 1.}, {-1., 1., 1.}, {-1.,-1., 1.}, 
  {-1.,-1.,-1.}, {-1., 1.,-1.}, { 1., 1.,-1.}, { 1.,-1.,-1.}
};

mesh := rdr.Mesh{
  mode:rdr.MODE_TRIG_LIST,
  vertices:cube_vertices,
  indices:list[i32]{
     0,  1,  2,  0,  2,  3,
     4,  5,  6,  4,  6,  7,
     8,  9, 10,  8, 10, 11,
    12, 13, 14, 12, 14, 15,
    16, 17, 18, 16, 18, 19,
    20, 21, 22, 20, 22, 23 
  },
  normals:list[vec[f32,3]]{
    { 1., 0.,0.}, { 1., 0.,0.}, { 1., 0.,0.}, { 1., 0.,0.},
    {-1., 0.,0.}, {-1., 0.,0.}, {-1., 0.,0.}, {-1., 0.,0.},
    { 0., 1.,0.}, { 0., 1.,0.}, { 0., 1.,0.}, { 0., 1.,0.},
    { 0.,-1.,0.}, { 0.,-1.,0.}, { 0.,-1.,0.}, { 0.,-1.,0.},
    { 0.,0., 1.}, { 0.,0., 1.}, { 0.,0., 1.}, { 0.,0., 1.},
    { 0.,0.,-1.}, { 0.,0.,-1.}, { 0.,0.,-1.}, { 0.,0.,-1.},
  },
  uvs:list[vec[f32,2]]{
    {0., 0.}, {0., 1.}, {1., 1.}, {1., 0.}, {0., 0.}, {0., 1.},
    {1., 1.}, {1., 0.}, {0., 0.}, {0., 1.}, {1., 1.}, {1., 0.},
    {0., 0.}, {0., 1.}, {1., 1.}, {1., 0.}, {0., 0.}, {0., 1.},
    {1., 1.}, {1., 0.}, {0., 0.}, {0., 1.}, {1., 1.}, {1., 0.}
  }
}

wire := rdr.Mesh{
  mode:rdr.MODE_LINE_LIST,
  vertices:cube_vertices,
  indices:list[i32]{
    0, 1, 1, 2, 2, 3, 3, 0,
    4, 5, 5, 6, 6, 7, 7, 4,
    2, 5, 1, 6, 0, 7, 3, 4
  },
};

cam := rdr.Camera{}

pixels := img.decode(io.read_file("examples/assets/pepper.png"));
image := frag.texture(...pixels.shape().yx);
image.write_pixels(pixels);

frame := 0;
lookx : f32 = 0;

while (1){
  time.fps(120);

  rdr.background(0.1);

  cam.look_at({0,0,8},{lookx,0,0},rdr.AXIS_Y);
  cam.perspective(60,W/(H as f32),0.1,100.0);

  frag.begin(shader0);
  cam.begin();
  mesh.draw(
    rdr.mat.translate(-2,-2,0) @*
    rdr.mat.rotate_deg(rdr.AXIS_Y,frame*2) @* 
    rdr.mat.rotate_deg(rdr.AXIS_X,frame*1)
  );
  cam.end();
  frag.end();

  frag.begin(shader1);
  frag.uniform("light",{0.1,0.2,0.3});
  cam.begin();
  mesh.draw(
    rdr.mat.translate(2,-2,0) @*
    rdr.mat.rotate_deg(rdr.AXIS_Y,frame*1) @* 
    rdr.mat.rotate_deg(rdr.AXIS_X,frame*2)
  );
  cam.end();
  frag.end();

  frag.begin(shader2);
  frag.uniform("image",image);
  cam.begin();
  mesh.draw(
    rdr.mat.translate(2,2,0) @*
    rdr.mat.rotate_deg(rdr.AXIS_Y,frame*2) @* 
    rdr.mat.rotate_deg(rdr.AXIS_Z,frame*1)
  );
  cam.end();
  frag.end();

  cam.begin();
  wire.draw(
    rdr.mat.translate(-2,2,0) @*
    rdr.mat.rotate_deg(rdr.AXIS_X,frame*2) @* 
    rdr.mat.rotate_deg(rdr.AXIS_Z,frame*1)
  );
  cam.end();

  rdr.flush();

  e := win.poll();
  if (e.type == win.MOUSE_MOVED){
    lookx = (e.x-(W/2))*0.01;
  }
  frame++;
}